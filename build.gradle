buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
    }
}
allprojects {
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "org.jetbrains.kotlin.jvm"
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
}
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    version = '1.0.7-alpha-dev-1'
    group 'top.wcpe.wcpelib'
    apply from: '../../build-upload.gradle'
    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
        from('src/main/java') {
            include '**/*.xml'
        }
        from('src/main/kotlin') {
            include '**/*.xml'
        }
    }
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    dependencies {
        implementation "redis.clients:jedis:3.6.0"
        implementation "log4j:log4j:1.2.17"
        implementation "org.slf4j:slf4j-nop:1.7.25"
        implementation "mysql:mysql-connector-java:5.1.35"
        implementation "org.mybatis:mybatis:3.5.6"
        implementation 'com.alibaba:druid:1.2.8'
        implementation "org.quartz-scheduler:quartz:2.3.0"
        implementation "com.google.code.gson:gson:2.8.9"
        implementation "com.squareup.okhttp3:okhttp:4.9.3"
        implementation "com.squareup.moshi:moshi:1.13.0"
        implementation "com.squareup.moshi:moshi-kotlin:1.13.0"

        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        testCompileOnly 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }
    test {
        useJUnitPlatform()
    }
}